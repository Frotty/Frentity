package FxEntity
import public Entity
import public Fx
import Assets

public constant DEFAULT_FX = Abilities.arrowMissile1

abstract public class FxEntity extends Entity
	protected effect fx

	construct(player owner, vec3 pos, real radius, angle xyAngle, string sfxpath)
		super(owner, pos, radius)
		setupFx(xyAngle, sfxpath)

	construct(player owner, vec3 pos, angle xyAngle, string sfxpath)
		super(owner, pos, radius)
		setupFx(xyAngle, sfxpath)

	construct(vec3 pos, player owner)
		super(owner, pos, 0.)
		setupFx(angle(0), DEFAULT_FX)

	private function setupFx(angle xyAngle, string sfxpath)
		fx = addEffect(sfxpath, pos)..setYaw(xyAngle)

	override function update()
		if pos.inPlayable()
			super.update()
			fx.setPos(pos)
		else
			terminate()

	function setFx(string sfxpath)
		fx.destr()
		fx = addEffect(sfxpath, pos)

	override function setPos(vec3 tpos)
		pos = tpos
		fx.setPos(tpos)

	override function setXY(vec3 tpos)
		pos = tpos
		fx.setPos(tpos)

	ondestroy
		if (silent)
			fx.setPos(vec2(2147483647, 2147483647))
		fx.destr()
