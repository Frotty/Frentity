package Encoder
import BigString
import BigNum
import SeqWorker
// Encodes numbers into ones with higher bases and represents them in the shortest possible way
// Credits: Pipedream
public constant maxInt = 2147483647
public constant maxHash = 1000

public class Encoder
	string charset
	int base
	real digits	= 0.	//logarithmic approximation
	BigNum bignum 

	construct(string charset)
		this.charset = charset
		this.base = charset.length()
		this.bignum = new BigNum(base)
	
	ondestroy
		destroy bignum

	function encode(int val, int max)
		digits = digits + log(max+1.,base+0.)
		bignum.mulSmall(max+1)
		bignum.addSmall(val)
	
	function decode(int max) returns int
		return bignum.divSmall(max+1)
	
	function isEmpty() returns boolean
		return bignum.isZero()
	
	function length() returns real
		return digits
	
	function clean()
		bignum.clean()
	
	function pad()
		BigNum_l cur = bignum.list
		BigNum_l prev = null
		int maxlen = R2I(1.0 + length())
		
		while cur != null
			prev = cur
			cur = cur.next
			maxlen--
			
		while maxlen > 0
			prev.next = new BigNum_l()
			prev = prev.next
			maxlen--
	
	
	function toString(BigString big, PayloadCallback onFinish) 
		BigNum_l cur = bignum.list
		print("toString")
		doSeq(2, (SeqCallback cb) -> begin
			if cur != null
				big.addString(itochar(cur.leaf))
				cur = cur.next
			else
				print("toString done")
				cb.terminate()
				onFinish.customData = big castTo int
				onFinish.doStep()
		end)
	
	function fromString(BigString bs, PayloadCallback onFinish)
		int i = 0
		BigNum_l cur = new BigNum_l()
		bignum.list = cur
		doSeq(5, (SeqCallback cb) -> begin
			cur.leaf = chartoi(bs.getString(i, 1))
			if i < bs.getLength()-1
				cur.next = new BigNum_l()
				cur = cur.next
				i++
			else
				cb.terminate()
				onFinish.doStep()
		end)
	
	function hash() returns int
		int hash = 0
		int x
		BigNum_l cur = bignum.list
		while cur != null
			x = cur.leaf
			hash = ModuloInteger(hash+79*hash div (x+1) + 293*x div (1+hash - (hash div base)*base) + 479,maxHash)
			cur = cur.next
		return hash

	function save(BigString bs, PayloadCallback onFinish)
		int hash
		clean()
		hash = hash()
		print("encodehash: " + hash.toString())
		nullTimer(() -> begin
			encode(hash,maxHash)
			clean()
			pad()
			toString(bs, onFinish)
		end)
		
	function load(BigString bs, PayloadCallback onFinish)
		fromString(bs, (PayloadCallback cb) -> begin
			let inputhash = decode(maxHash)
			clean()
			let h = hash()
			print("inputhash: " + inputhash.toString() + " h: " + h.toString())
			onFinish.customData = (inputhash == h).toInt()
			onFinish.doStep()
		end)
		

	function chartoi( string c ) returns int
		int i = 0
		string cs = charset
		int len = base
		while i < len and c != SubString(cs,i,i+1)
			i = i + 1
		return i

	function itochar( int i ) returns string
		return SubString(charset,i,i+1)
	
